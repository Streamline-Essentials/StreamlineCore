buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
        classpath "net.kyori:blossom:1.3.1"
    }
}

plugins {
    id("java")
    id("net.kyori.blossom") version "1.3.1"
    id("com.github.johnrengelman.shadow") version "7.1.2"
}

blossom {
    replaceToken("${{project.version}}", project.version)
}

allprojects {
    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "net.kyori.blossom"

    apply from: "$rootDir/dependencies.gradle"

    group = properties["group"]
    version = properties["version"]

    ext {
        pluginMain = properties["plugin.main"] == "default" ?
                "${project.group}.${project.name.toLowerCase()}.${project.name}" :
                properties["plugin.main"]
    }

    repositories {
        mavenCentral()
        // JitPack
        maven { url "https://jitpack.io" }
        maven { url "https://mvnrepository.com/artifact" }
        // OpenCollab
        maven { url "https://repo.opencollab.dev/maven-snapshots/" }
        maven { url "https://repo.opencollab.dev/maven-releases/"}
        // Velocity
        maven { url "https://repo.papermc.io/repository/maven-public/" }
        // Fabric
        maven { url "https://maven.fabricmc.net/" }
        maven { url "https://libraries.minecraft.net/" }
        // PlaceholderAPI
        maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
    }

    processResources {
        // Debugging: Print values
        doFirst {
            println "Version: ${project.version}, Name: ${project.name}, Main: ${project.ext.pluginMain}"
        }

        inputs.property("name", "${project.name}")
        inputs.property("version", "${project.version}")
        inputs.property("main", "${project.ext.pluginMain}")

        filesMatching("**/plugin.yml") {
            expand (
                    "name": "${project.name}",
                    "version": "${project.version}",
                    "main": "${project.ext.pluginMain}",
            )
        }

        filesMatching("**/StreamlineVelocity.**") {
            expand (
                    "name": "${project.name}",
                    "version": "${project.version}",
            )
        }
    }

    shadowJar {
        manifest {
            attributes(
                    "Implementation-Title": "$project.name",
                    "Implementation-Version": "$project.version"
            )
        }

        relocate("tv.quaint.thebase.lib", "net.streamline.thebase.lib")

        archiveFileName = "${project.name}-${project.version}.jar"

        minimize()
    }

    artifacts {
        archives shadowJar
    }
}

wrapper {
    gradleVersion = "8.3"
    distributionType = Wrapper.DistributionType.ALL
}
