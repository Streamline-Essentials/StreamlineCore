buildscript {
    repositories {
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
    }
    dependencies {
        classpath("gradle.plugin.com.github.johnrengelman:shadow:7.1.2")
        classpath("io.freefair.gradle:lombok-plugin:8.6")
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
//    id 'maven-publish'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'io.freefair.lombok'
//    apply plugin: 'maven-publish'

    apply from: rootDir.toString() + '/dependencies.gradle'

    group = properties['group']
    version = properties['version']

    ext {
        pluginMain = properties['plugin.main'] == 'default' ?
                project.group.toString() + '.' + project.name.toString().toLowerCase() + '.' + project.name.toString() :
                properties['plugin.main']
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
        mavenLocal()

        // Spigot / Bukkit
        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/central' }
        // Paper / Velocity
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        // Fabric
        maven { url 'https://maven.fabricmc.net/' }
        maven { url 'https://libraries.minecraft.net/' }

        // JitPack
        maven { url 'https://jitpack.io' }
        maven { url 'https://mvnrepository.com/artifact' }
        // OpenCollab
        maven { url 'https://repo.opencollab.dev/maven-snapshots/' }
        maven { url 'https://repo.opencollab.dev/maven-releases/'}

        // PlaceholderAPI
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    }

    processResources {
        // Debugging: Print values
        doFirst {
            println 'Version: ' + rootProject.version.toString() + ', Name: ' + rootProject.name.toString() + ', Main: ' + project.ext.pluginMain.toString()
        }

        inputs.property('name', rootProject.name.toString())
        inputs.property('version', rootProject.version.toString())
        inputs.property('main', project.ext.pluginMain.toString())
        outputs.dir(file('build/generated-src'))

        java.sourceCompatibility = JavaVersion.VERSION_11
        java.targetCompatibility = JavaVersion.VERSION_11

        filesMatching('**/plugin.yml') {
            expand (
                    'name': rootProject.name.toString(),
                    'version': rootProject.version.toString(),
                    'main': project.ext.pluginMain.toString(),
            )
        }
        filesMatching('**/streamline.properties') {
            expand (
                    'name': rootProject.name.toString(),
                    'version': rootProject.version.toString(),
            )
        }
        filesMatching('**/singularity.properties') {
            expand (
                    'name': rootProject.name.toString(),
                    'version': rootProject.version.toString(),
            )
        }
    }

    shadowJar {
        manifest {
            attributes(
                    'Implementation-Title': rootProject.name.toString(),
                    'Implementation-Version': rootProject.version.toString()
            )
        }

        relocate('com.github.Anon8281.universalScheduler', 'host.plas.bou.libs.universalScheduler')

        archiveFileName = project.name + '-' + project.version + '.jar'

//        minimize()
    }

    artifacts {
        archives shadowJar
    }
}

wrapper {
    gradleVersion = '8.9'
    distributionType = Wrapper.DistributionType.ALL
}
