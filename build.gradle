buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
    }
}

plugins {
    id('java')
    id('com.github.johnrengelman.shadow') version '7.1.2'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    apply from: rootDir.toString() + '/dependencies.gradle'

    group = properties['group']
    version = properties['version']

    ext {
        pluginMain = properties['plugin.main'] == 'default' ?
                project.group.toString() + '.' + project.name.toString().toLowerCase() + '.' + project.name.toString() :
                properties['plugin.main']
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
        mavenLocal()

        // Spigot / Bukkit
        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
            content {
                includeGroup 'org.bukkit'
                includeGroup 'org.spigotmc'
            }
        }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/central' }
        // Paper / Velocity
        maven { url 'https://repo.papermc.io/repository/maven-public/' }
        // Fabric
        maven { url 'https://maven.fabricmc.net/' }
        maven { url 'https://libraries.minecraft.net/' }

        // JitPack
        maven { url 'https://jitpack.io' }
        maven { url 'https://mvnrepository.com/artifact' }
        // OpenCollab
        maven { url 'https://repo.opencollab.dev/maven-snapshots/' }
        maven { url 'https://repo.opencollab.dev/maven-releases/'}

        // PlaceholderAPI
        maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    }

    processResources {
        // Debugging: Print values
        doFirst {
            println 'Version: ' + rootProject.version.toString() + ', Name: ' + rootProject.name.toString() + ', Main: ' + project.ext.pluginMain.toString()
        }

        inputs.property('name', rootProject.name.toString())
        inputs.property('version', rootProject.version.toString())
        inputs.property('main', project.ext.pluginMain.toString())

        filesMatching('**/plugin.yml') {
            expand (
                    'name': rootProject.name.toString(),
                    'version': rootProject.version.toString(),
                    'main': project.ext.pluginMain.toString(),
            )
        }

        filesMatching('**/StreamlineVelocity.**') {
            expand (
                    'name': project.name.toString(),
                    'version': project.version.toString(),
            )
        }
    }

    shadowJar {
        manifest {
            attributes(
                    'Implementation-Title': project.name.toString(),
                    'Implementation-Version': project.version.toString()
            )
        }

        relocate('tv.quaint.thebase.lib', 'net.streamline.thebase.lib')

        archiveFileName = project.name + '-' + project.version + '.jar'

//        minimize()
    }

    artifacts {
        archives shadowJar
    }
}

wrapper {
    gradleVersion = '8.3'
    distributionType = Wrapper.DistributionType.ALL
}
