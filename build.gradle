buildscript {
    repositories {
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
    }
    dependencies {
        classpath("gradle.plugin.com.github.johnrengelman:shadow:7.1.2")
        classpath("io.freefair.gradle:lombok-plugin:8.6")
    }
}

allprojects {
    version = rootProject.properties["version"]
    group = rootProject.properties["group"]
}

subprojects {
    apply plugin: "java"
    apply plugin: "com.github.johnrengelman.shadow"
    apply plugin: "io.freefair.lombok"
    apply plugin: "maven-publish"

    apply from: rootDir.toString() + "/dependencies.gradle"

    ext {
        pluginMain = rootProject.properties["plugin.main"] == "default" ?
                project.group.toString() + "." + project.name.toString().toLowerCase() + "." + project.name.toString() :
                properties["plugin.main"]
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        mavenCentral()
        mavenLocal()

        // Spigot / Bukkit
        maven {
            url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
            content {
                includeGroup "org.bukkit"
                includeGroup "org.spigotmc"
            }
        }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "https://oss.sonatype.org/content/repositories/central" }
        // Paper / Velocity
        maven {
            name = 'papermc'
            url = 'https://repo.papermc.io/repository/maven-public/'
        }
        // Fabric
        maven { url "https://maven.fabricmc.net" }
        maven { url "https://libraries.minecraft.net" }

        // JitPack
        maven { url "https://jitpack.io" }
        maven { url "https://mvnrepository.com/artifact" }
        // OpenCollab
        maven {
            name "opencollabRepositoryMavenSnapshots"
            url "https://repo.opencollab.dev/maven-snapshots"
        }
        maven {
            name "opencollabRepositoryMavenReleases"
            url "https://repo.opencollab.dev/maven-releases"
        }

        // PlaceholderAPI
        maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi" }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    processResources {
        // Debugging: Print values
        doFirst {
            println "Version: " + rootProject.version.toString() + ", Name: " + rootProject.name.toString() + ", Main: " + project.ext.pluginMain.toString()
        }

        inputs.property("name", rootProject.name.toString())
        inputs.property("version", rootProject.version.toString())
        inputs.property("main", project.ext.pluginMain.toString())
        outputs.dir(file("build/generated-src"))

        filesMatching("**/plugin.yml") {
            expand (
                    "name": rootProject.name.toString(),
                    "version": rootProject.version.toString(),
                    "main": project.ext.pluginMain.toString(),
            )
        }
        filesMatching("**/streamline.properties") {
            expand (
                    "name": rootProject.name.toString(),
                    "version": rootProject.version.toString(),
            )
        }
        filesMatching("**/singularity.properties") {
            expand (
                    "name": rootProject.name.toString(),
                    "version": rootProject.version.toString(),
            )
        }
    }

    shadowJar {
        manifest {
            attributes(
                    "Implementation-Title": rootProject.name.toString(),
                    "Implementation-Version": rootProject.version.toString()
            )
        }

        relocate("com.github.Anon8281.universalScheduler", "host.plas.bou.libs.universalScheduler")

        archiveFileName = project.name + "-" + project.version + ".jar"

//        minimize()
    }

    artifacts {
        archives shadowJar
    }

    tasks.register('deploy', Copy) {
        // Define the deployment directory
        def deployDir = file(System.getenv("DEPLOY_DIR") ?: "$rootDir/deploy")

        // Ensure the deployment directory exists
        doFirst {
            println "Deploying to: $deployDir"
            deployDir.mkdirs()
            println "Generated JAR file: ${shadowJar.archiveFile}"
        }

        // Copy the shadowJar output
        from shadowJar.archiveFile
        into deployDir
    }

    // Ensure that the deploy task runs after the shadowJar task
    shadowJar.finalizedBy(deploy)

    tasks.named('deploy').configure {
        dependsOn 'shadowJar', 'sourcesJar', 'javadocJar', 'jar'
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId project.group
                artifactId project.name
                version project.version
                from components.java
            }
        }
    }
}

wrapper {
    gradleVersion = "8.9"
    distributionType = Wrapper.DistributionType.ALL
}